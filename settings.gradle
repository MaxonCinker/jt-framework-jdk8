pluginManagement {
    repositories {
        mavenLocal()
        [
                'https://maven.aliyun.com/repository/google',
                'https://maven.aliyun.com/repository/public',
                'https://maven.aliyun.com/repository/gradle-plugin',
                'https://maven.aliyun.com/nexus/content/repositories/jcenter',
                'https://maven.aliyun.com/nexus/content/groups/public/',
                "https://plugins.gradle.org/m2/",
                "https://repo.spring.io/release",
                "https://repo.spring.io/snapshot",
        ].each { repoUrl ->
            maven { url repoUrl }
        }
        gradlePluginPortal()
        mavenCentral()
    }
//    resolutionStrategy {
//        eachPlugin {
//            if (requested.id.id == "org.jetbrains.kotlin.jvm") {
//                useVersion "${kotlinVersion}"
//            }
//            if (requested.id.id == "org.jetbrains.kotlin.plugin.spring") {
//                useVersion "${kotlinVersion}"
//            }
//        }
//    }
}

// build-script
include 'build-script'

// modules
include 'jt-core'
include 'jt-spring'
include 'jt-808-server-support'
include 'jt-808-server-spring-boot-autoconfigure'
include 'jt-808-server-spring-boot-starter-boot2'
include 'jt-808-server-spring-boot-starter'

// samples
include 'samples'
include 'samples:jt-808-server-sample-debug'
include 'samples:jt-808-server-sample-bare'
include 'samples:jt-808-server-sample-bare-boot3'
include 'samples:jt-808-server-sample-customized'
include 'samples:jt-808-server-sample-annotation'
include 'samples:jt-808-client-sample-debug'

rootProject.name = "jt-framework"
// Change default "build.gradle" file to "${project.name}.gradle" recursively
setBuildFileName(rootProject)

def setBuildFileName(project) {
    project.children.each { childProject ->
        childProject.buildFileName = "${childProject.name}.gradle"
        assert childProject.projectDir.isDirectory()
        assert childProject.buildFile.isFile()
        setBuildFileName(childProject)
    }
}
